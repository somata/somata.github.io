<html><head><link rel="stylesheet" href="/css/base.css"/></head><body><div id="header"><div class="container"><img src="/images/logo.png" class="logo"/><h1>somata</h1><h2>the minimal microservice framework</h2></div></div><div id="navigation"><div class="container"><li><a href="/">Intro</a></li><li><a href="/download">Download</a></li><li><a href="/examples" class="selected">Examples</a></li><li><a href="/documentation">Documentation</a></li></div></div><div id="content"><div class="container"><h2>A simple Somata service with Node.js</h2><p>A Somata service exposes methods that can be called by a Somata client elsewhere in your code. Let's make a simple SpeakerService that exposes two methods to say hello and goodbye.</p><pre>somata = require 'somata'
 
# Create a new Somata service named 'speaker'
SpeakerService = new somata.Service 'speaker', {
 
    # With a few methods
    sayHello: (name, cb) ->
        cb null, 'Hello, ' + name + '!'
 
    sayGoodbye: (name, cb) ->
        cb null, 'Goodbye, ' + name + '!'
}</pre><p>Now let's call these functions in order from another piece of code.</p><pre>somata = require 'somata'
 
# Create a new Somata client
client = new somata.Client
 
# Execute the SpeakerService's `sayHello` method with argument 'world'
client.remote 'speaker', 'sayHello', 'world', (err, hello) ->
    console.log '[speaker.sayHello] response: ' + hello
 
    # ... then execute SpeakerService.sayGoodbye('world')
    client.remote 'speaker', 'sayGoodbye', 'world', (err, goodbye) ->
        console.log '[speaker.sayGoodbye] response: ' + goodbye
        process.exit()</pre><p>Start the service, then run the client:</p><pre>$ coffee speaker-service.coffee &
Somata service listening on localhost:15555...
 
$ coffee speaker-client.coffee
Found service speaker@localhost:15555
[speaker.sayHello] response: Hello, world!
[speaker.sayGoodbye] response: Goodbye, world!</pre><h2>Subscribe to a remote event</h2><p>Each Somata service can also publish events. Any client in the 'cluster/system' can then subscribe to these events as you would with any javascript event. Let's create a Service that will publish an 'announcement' with a message every second and print out the message elsewhere by subscribing to the 'announcement' event.</p><pre>somata = require 'somata'
 
Publisher = new somata.Service 'publisher'
 
# Publish an 'announcement' event with message 'Hey there, world!'
sendAnnouncement = ->
    Publisher.publish 'announcement', 'Hey there, world!'
 
# Publish every second
setInterval sendAnnouncement, 1000</pre><p>Create a Client that subscribes to the 'announcement' event and logs the message</p><pre>somata = require 'somata'
 
subscriber_client = new somata.Client
 
# Subscribe to an 'announcement' event from the 'publisher' service
subscriber_client.on 'publisher', 'announcement', (message) ->
    console.log '[subscriber.onAnnouncement] New message:', message</pre><p>Start the service and client</p><pre>$ coffee publisher-service.coffee &
[didBind] Socket mulj67id bound to tcp://0.0.0.0:36206...
Registered service `publisher~ho5xqalh` on tcp://0.0.0.0:36206
 
$ coffee subscriber-client.coffee
[Client.subscribe] publisher~u3sg0zoj : announcement<!-- - TODO: doesn't show stuff in brackets-->
Subscribing <mic9pdgi::publisher:announcementy7wq>
[subscriber.onAnnouncement] New message: Hey there, world!
[subscriber.onAnnouncement] New message: Hey there, world!
[subscriber.onAnnouncement] New message: Hey there, world!
...</pre><p>You can even subscribe to Services' events from the browser with <a href='https://github.com/somata/somata-socketio'>somata-socketio</a>.</p><h2>Connect an API and database server</h2><p>One Somata service will run the API, another the database. We'll use a Somata client to make database queries from within two different fetch routes. In deployment the API and data services can be run on the same machine or with each on their own specialized hardware. This also allows for nifty patterns such as multiple APIs to balance page rendering load under heavy traffic, with a single monolithic database service.</p><p>First a simple REST API with a few routes built with express.</p><!-- - TODO: link to a fuller polar example: 'For more on an approach to a simple... much TODO about nothing--><!-- - TODO: Another cool idea is a 'theoretical' Maia implementation that subscribes to incoming temperatures in the engine service, then sends a text with Twilio if the temperature is below a certain threshold. Could get some cross-pollination of marketing--><pre>somata = require 'somata'
express = require 'express'
 
# Create a new Somata client, use bindRemote shorthand for cleaner calls
client = new somata.Client
Data = client.bindRemote 'example:data'
 
app = express()
 
# Fetch users with a query from the request
app.get '/users.json', (req, res) ->
    Data 'findUsers', req.query, (err, users) ->
        res.json users
 
# Fetch all messages for a user
app.get '/users/:user_id/messages.json', (req, res) ->
    Data 'findMessages', {user_id: req.params.user_id}, (err, messages) ->
        res.json messages</pre><p>A data service with a json database in memory</p><h2>Somata across platforms</h2><p>For more examples, see <a href="https://github.com/somata/somata-node/tree/master/examples">https://github.com/somata/somata-node/tree/master/examples</a></p></div></div><div id="footer"><div class="container"><span>&copy; 2015 <a href="//prontotype.us/">Prontotype</a></span></div></div></body></html>